feature ignore_unknown_macro

server s1 -repeat 4 {
    rxreq
    txresp
} -start

haproxy h1 -conf {
    global
    .if streq("$VTC_SOCK_TYPE",quic)
        # required for backend connections
        expose-experimental-directives
    .endif
    .if feature(THREAD)
        thread-groups 1
    .endif

    .if !ssllib_name_startswith(AWS-LC)
        tune.ssl.default-dh-param 2048
    .endif
        crt-base ${testdir}/certs
        stats socket "${tmpdir}/h1/stats" level admin

    defaults
        mode http
        option httplog
        log stderr local0 debug err
        option logasap
        timeout connect "${HAPROXY_TEST_TIMEOUT-5s}"
        timeout client  "${HAPROXY_TEST_TIMEOUT-5s}"
        timeout server  "${HAPROXY_TEST_TIMEOUT-5s}"


    listen clear-lst
        bind "fd@${clearlst}"
        balance roundrobin
        server s1 "${VTC_SOCK_TYPE}+${h1_ssl_sock}" ssl verify none sni str(record1.bug940.domain.tld)
        server s2 "${VTC_SOCK_TYPE}+${h1_ssl_sock}" ssl verify none sni str(record2.bug940.domain.tld)
        server s3 "${VTC_SOCK_TYPE}+${h1_ssl_sock}" ssl verify none sni str(record3.bug940.domain.tld)
        server s4 "${VTC_SOCK_TYPE}+${h1_ssl_sock}" ssl verify none sni str(record4.bug940.domain.tld)

    listen ssl-lst
        mode http
        bind "${VTC_SOCK_TYPE}+fd@${ssl}" ssl strict-sni crt-list ${testdir}/certs/simple.crt-list

        server s1 ${s1_addr}:${s1_port}
} -start


client c1 -repeat 4 -connect ${h1_clearlst_sock} {
    txreq
    rxresp
    expect resp.status == 200
} -run
